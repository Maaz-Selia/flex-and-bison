<program> ::= <identifier> ":" <block> ENDP <identifier> "."

<block> ::= DECLARATIONS <declaration_block> CODE <statement_list> 
			| CODE <statement_list>

<identifier> ::= <character> 
				| <identifier> <character> 
				| <identifier> <digit>

<identifier_list> ::= <identifier> 
					| <identifier_list> "," <identifier>

<declaration_block> ::= <identifier_list> OF TYPE <type> ";" 
						| <declaration_block> <identifier_list> OF TYPE <type> ";"

<type> ::= CHARACTER 
		| INTEGER 
		| REAL

<statement_list> ::= <statement> 
					| <statement_list> ";" <statement>

<statement> ::= <assignment_statement>
            | <if_statement> 
			| <do_statement> 
			| <while_statement> 
			| <for_statement> 
			| <write_statement> 
			| <read_statement>

<assignment_statement> ::= <expression> "->" <identifier>

<if_statement> ::= IF <conditional> THEN <statement_list> ENDIF
                 | IF <conditional> THEN <statement_list> ELSE <statement_list> ENDIF

<do_statement> ::= DO <statement_list> WHILE <conditional> ENDDO

<while_statement> ::= WHILE <conditional> DO <statement_list> ENDWHILE

<for_conditions> ::= IS <expression> BY <expression> TO <expression>

<for_statement> ::= FOR <identifier> <for_conditions> DO <statement_list> ENDFOR

<write_statement> ::= WRITE "(" <output_list> ")" | NEWLINE

<read_statement> ::= READ "(" <identifier> ")"

<output_list> ::= <value> 
				| <output_list> "," <value>

<logical_operator> ::= AND 
					| OR

<basic_conditional> ::= <expression> <comparator> <expression> 
						| NOT <basic_conditional>

<conditional> ::= <basic_conditional> 
				| <conditional> <logical_operator> <basic_conditional>

<comparator> ::= "=" 
				| "<>" 
				| "<" 
				| ">" 
				| "<=" 
				| ">="

<expression> ::= <term> 
				| <expression> "+" <term> 
				| <expression> "-" <term>

<term> ::= <value> 
		| <term> "*" <value> 
		| <term> "/" <value>

<value> ::= <identifier> 
			| <constant> 
			| "(" <expression> ")"

<constant> ::= <number_constant> 
			| <character_constant>
			
<character> ::= a | b | c | d | e | f | g | h | i | j | k | l | m | n | o 
				| p | q | r | s | t | u | v | w | x | y | z | A | B | C | D 
				| E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S 
				| T | U | V | W | X | Y | Z
				
<character_constant> ::= "'" <character> "'"

<digit> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9

<integer> ::= <digit> | <digit> <integer>

<number_constant> ::= <integer> | "-" <integer>
					| <integer> "." <integer>
					| "-" <integer> "." <integer>